<html>
<head>

<script id='lineVertex' type='x-shader/x-vertex'>
precision mediump float;
attribute vec2 aCoord;

void main() {
    gl_Position = vec4(aCoord, 0., 1.);
}
</script>

<script id='lineFragment' type='x-shader/x-fragment'>
void main() {
    gl_FragColor = vec4(vec3(0.), 0.7);
}
</script>

<script id='effectVertex' type='x-shader/x-vertex'>
precision mediump float;
attribute vec2 aCoord;
varying vec2 vCoord;

void main() {
    gl_Position = vec4(aCoord, 0., 1.);
    vCoord = (aCoord + vec2(1.)) / 2.;
}
</script>

<script id='effectFragment' type='x-shader/x-fragment'>
precision mediump float;
uniform sampler2D uTexture;
uniform sampler2D uLineTexture;
uniform sampler2D uNoiseTexture;
uniform vec3 uColorShift;
uniform vec3 uColorLevel;
uniform vec3 uColorWeight;
uniform vec2 uNoiseShift;
uniform vec3 uNoiseLevel;
uniform vec3 uNoiseColor;
uniform float uSlitscanResolution;
uniform float uTime;
uniform vec2 uAdjustRepeat;
uniform vec2 uAdjustOffset;
uniform vec2 uTubeAdjust;
varying vec2 vCoord;

void main() {
    vec2 adjustCoord = mod(vCoord * uAdjustRepeat + uAdjustOffset, vec2(1.));
    vec4 lineMask = texture2D(uLineTexture, vec2(0, vCoord.y));
    vec2 coord = vec2(
            floor((adjustCoord.x - lineMask.a) * uSlitscanResolution) /
                    uSlitscanResolution,
            adjustCoord.y);
    float r = texture2D(uTexture, vec2(coord.x + uColorShift.r, coord.y)).r;
    float g = texture2D(uTexture, vec2(coord.x + uColorShift.g, coord.y)).g;
    float b = texture2D(uTexture, vec2(coord.x + uColorShift.b, coord.y)).b;
    float y = 0.299 * r + 0.587 * g + 0.114 * b;
    vec3 rgb =
            (vec3(y) + (vec3(r, g, b) - vec3(y)) * uColorLevel) * uColorWeight;
    vec4 noiseTexture = texture2D(uNoiseTexture, mod(vCoord + uNoiseShift, 1.));
    vec4 fixedNoiseTexture = texture2D(uNoiseTexture, vCoord);
    float whiteNoise = noiseTexture.r * uNoiseLevel.r;
    float pinkNoise = noiseTexture.g * uNoiseLevel.g;
    float parlinNoise1 = fixedNoiseTexture.b;
    float parlinNoise2 = fixedNoiseTexture.a;
    float s = (sin(uTime) + 1.) / 2.;
    float parlinNoise =
            (parlinNoise1 * s + parlinNoise2 * (1. - s)) * uNoiseLevel.b;
    vec3 lineNoise = vec3(lineMask.a) * lineMask.rgb * 2.;
    vec3 color = rgb * lineMask.rgb + lineNoise +
            vec3(whiteNoise + pinkNoise + parlinNoise) * uNoiseColor;
    vec2 grad = vCoord * (vec2(1.0, 1.0) - vCoord);
    float tube = 1.0 * uTubeAdjust.x + grad.x * grad.y * uTubeAdjust.y;
    gl_FragColor = vec4(color * vec3(tube), 1.);
}
</script>

</head>
</html>
